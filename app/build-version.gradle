ext {
    versionFile = new File(project.rootDir, 'build-version.properties')
    calculateVersionName = {
        def version = readVersion()
        return "${version['major']}.${version['minor']}.${version['patch']}.${version['build']}"
    }

    calculateVersionCode = {
        def version = readVersion()
        def major = version['major'] as int // 1..âˆž
        def minor = version['minor'] as int // 0..99
        def patch = version['patch'] as int // 0..999
        def build = version['build'] as int // 0..9999
        def vCode = (10000000*major) + (100000*minor) + (10000*patch) + build as int
        return vCode
    }
    getKickoff = {
        return ( System.env.kickoff == "null") ? "undef"  : System.env.kickoff
    }
    getEnv =  {
        return ( System.env.environment == "null" ) ? "undef"  : System.env.environment
    }
}



Properties readVersion() {
    def version = new Properties()
    def stream
    try {
        stream = new FileInputStream(versionFile)
        version.load(stream)
    } catch (FileNotFoundException ignore) {
    } finally {
        if (stream != null) stream.close()
    }
    // safety defaults in case file is missing
    if(!version['major']) version['major'] = "4"
    if(!version['minor']) version['minor'] = "0"
    if(!version['patch']) version['patch'] = "0"
    if(!version['build']) version['build'] = "6"
    return version
}

void incrementVersionNumber() {
    def version = readVersion()

    // careful with the types, culprits: "9"++ = ":", "9" + 1 = "91"
    def build = version['build'] as int
    build++
    version['build'] = build.toString()

    def stream = new FileOutputStream(versionFile)
    try {
        version.store(stream, null)
    } finally {
        stream.close()
    }
}

task incrementVersion {
    description "Increments build counter in ${versionFile}"
    doFirst {
        incrementVersionNumber()
    }
}

if (plugins.hasPlugin('android') || plugins.hasPlugin('android-library')) {
    android {
        defaultConfig {
            versionName = calculateVersionName()
            versionCode = calculateVersionCode()
        }

        afterEvaluate {
            def autoIncrementVariant = { variant ->
                if (variant.buildType.name == buildTypes.release.name) { // don't increment on debug builds
                    variant.preBuild.dependsOn incrementVersion
                    incrementVersion.doLast {
                        variant.mergedFlavor.versionName = calculateVersionName()
                        variant.mergedFlavor.versionCode = calculateVersionCode()
                    }
                }
            }
            if (plugins.hasPlugin('android')) {
                applicationVariants.all { variant -> autoIncrementVariant(variant) }
            }
            if (plugins.hasPlugin('android-library')) {
                libraryVariants.all { variant -> autoIncrementVariant(variant) }
            }
        }
    }
}
